
    model{
   
  
    #---------------------------------------------------------------------------
    # Priors        
    #---------------------------------------------------------------------------
    for(j in 1:obs_wnode_n)
    {
      for(i in 1:obs_bnode_n){
          conn_g[i,j] <- alpha[i,j]/sum(alpha)
          alpha[i,j] ~ dpois(lambda[i,j])
          lambda[i,j] ~ dunif(0,1000)
        }
    }
 
 
    
    #---------------------------------------------------------------------------
    # Part 1. The data in colums are the number of individuals trapped at each       
    # non-breeding node. these are assumed to be multinomial.  
    #---------------------------------------------------------------------------
    for(j in 1:obs_wnode_n)
    {
        dta_conn_y[1:obs_bnode_n,j] ~ dmulti(conn_g[,j], dta_conn_colsum[j]) 
    } 
    #---------------------------------------------------------------------------
    # Part 2 The data in rows can also be assumed to be mutinomial but we have to       
    # adjust for the fact that there was different effort at each winter node so       
    # the probabilities of the multinomial are multiplied by relative effort 
    # which is  estimated as proportion of total no. of birds sampled. Have to use a        
    # different variable name, dta_conn_x which is an exact copy of dta_conn_y
    #---------------------------------------------------------------------------
    for (i in 1:obs_bnode_n){
        dta_conn_x[i,1:obs_wnode_n] ~ dmulti(px[i,], dta_conn_rowsum[i])
        for (j in 1:obs_wnode_n){
            px[i,j] <- conn_g[i,j]*dta_conn_effort[j]
        } #j
    } #i

    #---------------------------------------------------------------------------
    # Part 3. Assume the breeding population indices (mut be integers) are mutinomial 
    # dta_conn_nb.est.sum is the sum of dta_conn_nb.est.mn 
    #---------------------------------------------------------------------------
    for(i in 1:obs_bnode_n)
    {
        pb[i]<-sum(conn_g[i,])
    }
    dta_conn_nb.est.mn ~ dmulti(pb,dta_conn_nb.est.sum)

    #---------------------------------------------------------------------------
    # Part 4 Assume the winter population indices (mut be integers) are mutinomial
    #---------------------------------------------------------------------------
    for(j in 1:obs_wnode_n)
    {
        pw[j]<-sum(conn_g[,j])
     }
    dta_conn_nw.est.mn ~ dmulti(pw,dta_conn_nw.est.sum)
    }
    
